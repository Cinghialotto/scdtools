#!/usr/bin/perl
###############################################################################
# Copyright (c) 2012 by bgvanbur
#
# This program is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by the
# Free Software Foundation; either version 2 of the License, or (at your
# option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
###############################################################################

# TODO support bin/cue format?

use strict;
use warnings;

use POSIX qw(SEEK_SET);

my $verbosity = 2;
my $ipFile = 'IP.BIN';
my $spFile = 'SP.BIN';
my $iso = '';
my $removeTrailingZeros = 0;

foreach my $arg (@ARGV) {
    if ( $arg =~ m/^-v=(\d+)$/i ) {
	$verbosity = $1;
    } elsif ( $arg =~ m/^-ipfile=(.+)$/i ) {
	$ipFile = $1;
    } elsif ( $arg =~ m/^-spfile=(.+)$/i ) {
	$spFile = $1;
    } elsif ( $arg =~ m/^-iponly$/i ) {
	$spFile = '';
    } elsif ( $arg =~ m/^-sponly$/i ) {
	$ipFile = '';
    } elsif ( $arg =~ m/^-removetrailingzeros$/i ) {
	$removeTrailingZeros = 1;
    } elsif ( $iso eq '' && -e $arg ) {
	$iso = $arg;
    } else {
	print "Bad argument: $arg\n";
	&Help();
    }
}

if ( $iso eq '' ||
     ( $ipFile eq '' && $spFile eq '' ) ) {
    &Help();
}

if (!open(ISO, $iso)) {
    die "Could not open ISO: $iso\n";
}

binmode ISO;

# read values from System ID
my $ipSizeID = &ReadLong(0x34);
my $spStart  = &ReadLong(0x40);
my $spSize   = &ReadLong(0x44);

# determine proprietary code size since affects IP start and size
my $propSize = &DetermineProprietarySize();

# determine IP start and size accounting for proprietary code
my $ipStart  = 0x200+$propSize;
my $ipSize   = (0x600-$propSize) + $ipSizeID;

# read the IP and SP data
my $ipData = &ReadString($ipStart,$ipSize);
my $spData = &ReadString($spStart,$spSize);

close ISO;

if ( $removeTrailingZeros ) {
    $ipData =~ s/\x00+$//;
    my $ipSizeNew = length($ipData);
    my $ipSizeDiff = $ipSize - $ipSizeNew;
    if ( $ipSizeDiff ) {
	print "removed $ipSizeDiff zero bytes from the end of $ipFile\n";
	$ipSize = $ipSizeNew;
    }

    $spData =~ s/\x00+$//;
    my $spSizeNew = length($spData);
    my $spSizeDiff = $spSize - $spSizeNew;
    if ( $spSizeDiff ) {
	print "removed $spSizeDiff zero bytes from the end of $spFile\n";
	$spSize = $spSizeNew;
    }
}

# now just make the IP.BIN and SP.BIN from the data extracted
&OutputFileFromArea($ipFile,$ipStart,$ipSize,$ipData);
&OutputFileFromArea($spFile,$spStart,$spSize,$spData);



sub OutputFileFromArea {
    my ($file,$offset,$size,$data) = @_;
    if ( $file ne '' ) {
	printf("Making %s from ISO offset \$%8.8X with size \$%8.8X\n",$file,$offset,$size);
	if ( ! open(OUTPUT,'>'.$file) ) {
	    die "Can write file: $file\n";
	}
	print OUTPUT $data;
	close OUTPUT;
    }
}

sub ReadString {
    my ($offset,$size) = @_;
    seek(ISO,$offset,SEEK_SET);
    my $data = chr(0x00)x$size;
    if ( read(ISO,$data,$size) != $size ) {
	die "Could not read ISO data\n";
    }
    return $data;
}

sub ReadLong {
    my $data = &ReadString($_[0],4);
    return unpack("N",$data);
}

sub ReadByte {
    my $data = &ReadString($_[0],1);
    return ord($data);
}

sub DetermineProprietarySize {
    # determine proprietary code size by examining the contents of it
    # the first different byte between all three regions is 0x20B
    # 0x7A (U)
    # 0xA1 (J)
    # 0x64 (E)
    my $data = &ReadByte(0x20B);
    my $size = 0;
    if ( $data == 0x7A ) {
	$size = 0x584;
    } elsif ( $data == 0xA1 ) {
	$size = 0x156;
    } elsif ( $data == 0x64 ) {
	$size = 0x56E;
    } else {
	die "Could not determine region of ISO\n";
    }
}



sub Help {
    die '
scdextractipsp <iso>

[description]
  Extracts IP data and SP data from a Sega CD ISO.  

[options]
  -ipfile=<file>        Name of file to write IP data (defaults IP.BIN)
  -spfile=<file>        Name of file to write SP data (defaults SP.BIN)
  -iponly               Only extract IP data
  -sponly               Only extract SP data
  -removetrailingzeros  remove the trailing zero bytes at the end of IP/SP
                        most commercial games had an IP/SP that was mostly zero

';
}
